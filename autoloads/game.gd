class_name Game
extends Node

const SAVE_PATH = "user://game.save"
const CONFIG_PATH = "user://game.cfg"

signal players_updated
signal player_updated(id: int)
signal vote_updated(id: int)

static var instance: Game

@export var multiplayer_test: bool = false
@export var use_roles: bool = true
@export var unique_roles: bool = true # won't start with repeated roles
@export var all_roles: bool = true # won't start if all roles aren't selected
@export var min_players: int = 2 # won't start if there are at least these players
@export var fill_screen: bool = true
@export var test_players: Array[PlayerDataResource] = [] # first one is server
@export var main_scene: PackedScene

var players: Array[Statics.PlayerData] = []
var change_window_scale : bool = true :
	set(value):
		var last_value: bool = change_window_scale
		change_window_scale = value
		if not change_window_scale:
			reset_window_scale()
		elif last_value != value:
			_update_window_scale()


var _is_window_small: bool = false
var _initial_window_scale_mode: Window.ContentScaleMode
var _initial_window_scale_aspect: Window.ContentScaleAspect

@onready var player_id_label: Label = %PlayerIdLabel

func _enter_tree() -> void:
	instance = self

func _ready() -> void:
	_initial_window_scale_mode = get_window().content_scale_mode
	_initial_window_scale_aspect = get_window().content_scale_aspect
	
	get_window().size_changed.connect(_handle_size_changed)
	_update_window_scale()
	get_tree().node_added.connect(_handle_node_added)
	
	if not OS.is_debug_build():
		multiplayer_test = false
		player_id_label.hide()
	
	if not FileAccess.file_exists(CONFIG_PATH):
		save_config()
	load_config()


func sort_players() -> void:
	players.sort_custom(func(a: Statics.PlayerData, b: Statics.PlayerData) -> bool: return a.index < b.index)


func add_player(player: Statics.PlayerData) -> void:
	var existing_player: Statics.PlayerData = null
	for data: Statics.PlayerData in players:
		if data.id == player.id:
			existing_player = data
			break
	if existing_player:
		existing_player.update(player)
	else:
		players.append(player)
	sort_players()
	players_updated.emit()


func remove_player(id: int) -> void:
	for i: int in players.size():
		if players[i].id == id:
			players.remove_at(i)
			break
	
	if multiplayer.is_server():
		var player_indices: Dictionary = {}
		for i: int in players.size():
			players[i].index = i
			player_indices[players[i].id] = i
		update_indices.rpc(player_indices)
	players_updated.emit()


func get_player(id: int) -> Statics.PlayerData:
	for player: Statics.PlayerData in players:
		if player.id == id:
			return player
	return null


func get_current_player() -> Statics.PlayerData:
	return get_player(multiplayer.get_unique_id())


func get_player_by_name(player_name: String) -> Statics.PlayerData:
	for player: Statics.PlayerData in players:
		if player.name == player_name:
			return player
	return null


@rpc("reliable")
func update_indices(player_indices: Dictionary) -> void:
	for player: Statics.PlayerData in Game.instance.players:
		if player.id in player_indices:
			player.index = player_indices[player.id]
			if player.id == multiplayer.get_unique_id():
				Debug.index = player.index
				Debug.add_to_window_title("Client %d" % player.index)
	sort_players()
	players_updated.emit()


@rpc("any_peer", "reliable", "call_local")
func set_player_role(id: int, role: Statics.Role) -> void:
	var player: Statics.PlayerData = get_player(id)
	player.role = role
	player_updated.emit(id)


func set_current_player_role(role: Statics.Role) -> void:
	set_player_role.rpc(multiplayer.get_unique_id(), role)


@rpc("any_peer", "reliable", "call_local")
func set_player_vote(id: int, vote: bool) -> void:
	var player: Statics.PlayerData = get_player(id)
	if not player:
		return
	player.vote = vote
	player_updated.emit(id)
	vote_updated.emit(id)


func set_current_player_vote(vote: bool) -> void:
	set_player_vote.rpc(multiplayer.get_unique_id(), vote)


func reset_votes() -> void:
	for player: Statics.PlayerData in players:
		set_player_vote.rpc(player.id, false)


func is_online() -> bool:
	return not multiplayer.multiplayer_peer is OfflineMultiplayerPeer and \
		multiplayer.multiplayer_peer.get_connection_status() != MultiplayerPeer.CONNECTION_DISCONNECTED


func update_player_id() -> void:
	if not OS.is_debug_build():
		return
	if Debug.is_online():
		player_id_label.show()
		player_id_label.text = str(multiplayer.get_unique_id())
	else:
		player_id_label.hide()


func reset_window_scale() -> void:
	get_window().content_scale_mode = _initial_window_scale_mode
	get_window().content_scale_aspect = _initial_window_scale_aspect


func add_coins(player_id: int, amount: int) -> void:
	if not multiplayer.is_server():
		return
	var player_data: Statics.PlayerData = get_player(player_id)
	player_data.coins += amount
	coins_broadcast.rpc(player_id, player_data.coins)


@rpc()
func coins_broadcast(player_id: int, coins: int) -> void:
	var player_data: Statics.PlayerData = get_player(player_id)
	player_data.coins = coins


func save_game() -> void:
	var dict: Dictionary = {
		"level": 1,
		"players": {}
	}
	
	for player_data: Statics.PlayerData in players:
		dict.players[player_data.name] = {
			"coins": player_data.coins
		}
	
	var string: String = JSON.stringify(dict)
	var file: FileAccess = FileAccess.open_encrypted_with_pass(SAVE_PATH, FileAccess.WRITE, "1234")
	file.store_string(string)
	file.close()


func load_game() -> void:
	var file: FileAccess = FileAccess.open_encrypted_with_pass(SAVE_PATH, FileAccess.READ, "1234")
	var string: String = file.get_as_text()
	file.close()
	var dict: Dictionary = JSON.parse_string(string)
	for key: String in dict.players:
		var player_data: Statics.PlayerData = get_player_by_name(key)
		if not player_data:
			continue
		player_data.coins = dict.players[key].coins
		coins_broadcast.rpc(player_data.id, dict.players[key].coins)


func save_config() -> void:
	var config_file: ConfigFile = ConfigFile.new()
	config_file.set_value("Video", "fullscreen", get_window().mode == Window.MODE_FULLSCREEN)
	config_file.set_value("Audio", "music", AudioServer.get_bus_volume_linear(AudioServer.get_bus_index("Music")))
	config_file.set_value("Audio", "sfx", AudioServer.get_bus_volume_linear(AudioServer.get_bus_index("SFX")))
	config_file.save(CONFIG_PATH)


func load_config() -> void:
	var config_file: ConfigFile = ConfigFile.new()
	var err: Error = config_file.load(CONFIG_PATH)
	if err != OK:
		return
	get_window().mode = Window.MODE_FULLSCREEN if config_file.get_value("Video", "fullscreen", false) else Window.MODE_WINDOWED
	AudioServer.set_bus_volume_linear(AudioServer.get_bus_index("Music"), config_file.get_value("Audio", "music"))
	AudioServer.set_bus_volume_linear(AudioServer.get_bus_index("SFX"), config_file.get_value("Audio", "sfx"))



func _handle_size_changed() -> void:
	if not change_window_scale:
		return
	
	var was_windows_small: bool = _is_window_small
	#get_window().min_size = Vector2i(1280, 720)
	_is_window_small =  get_window().size.x < 1280 or get_window().size.y < 720

	if was_windows_small == _is_window_small:
		return
	
	_update_window_scale()


func _update_window_scale() -> void:
	if _is_window_small:
		get_window().content_scale_mode = Window.CONTENT_SCALE_MODE_CANVAS_ITEMS
		get_window().content_scale_aspect = Window.CONTENT_SCALE_ASPECT_EXPAND
	else:
		get_window().content_scale_mode = Window.CONTENT_SCALE_MODE_DISABLED
		get_window().content_scale_aspect = Window.CONTENT_SCALE_ASPECT_KEEP


func _handle_node_added(node: Node) -> void:
	if node.get_parent() == get_window():
		# Scene has been changed
		change_window_scale = node is MainMenu or node is LobbyHostScreen or \
			node is LobbyJoinScreen or node is LobbyWaitingScreen or node is Credits
